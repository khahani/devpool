import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.google.android.libraries.mapsplatform.secrets-gradle-plugin:secrets-gradle-plugin:2.0.1"
    }
}

plugins {
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.22' apply false
    id 'com.diffplug.spotless' version '6.21.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.9.0' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0' apply false
    id "org.jetbrains.kotlin.kapt" version "1.9.0" apply false
    id 'com.google.dagger.hilt.android' version '2.47' apply false
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        lineEndings 'UNIX'
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            ktlint("0.50.0")
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            // Use experimental APIs
            freeCompilerArgs.add('-opt-in=kotlin.RequiresOptIn')
        }
    }
    // androidx.test and hilt are forcing JUnit, 4.12. This forces them to use 4.13
    configurations.configureEach {
        resolutionStrategy {
            force "junit:junit:4.13.2"
        }
    }
}
