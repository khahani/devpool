pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('lifecycle', '2.6.1')
            version('hilt', '2.48.1')
            version('okhttp3', '4.11.0')
            library('kotlin.stdlib', 'org.jetbrains.kotlin', 'kotlin-stdlib-jdk8').version('1.8.20')
            library('kotlinx.coroutines.android', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-android').version('1.7.3')
            library('androidx.activity.compose', 'androidx.activity', 'activity-compose').version('1.7.2')
            library('androidx.appcompat', 'androidx.appcompat', 'appcompat').version('1.6.1')
            library('androidx.lifecycle.viewmodel.compose', 'androidx.lifecycle', 'lifecycle-viewmodel-compose').versionRef('lifecycle')
            library('androidx.lifecycle.viewmodel.ktx', 'androidx.lifecycle', 'lifecycle-viewmodel-ktx').versionRef('lifecycle')
            library('androidx.lifecycle.runtime.compose', 'androidx.lifecycle', 'lifecycle-runtime-compose').versionRef('lifecycle')
            library('androidx.core.ktx', 'androidx.core', 'core-ktx').version('1.9.0')
            library('hilt.android.android', 'com.google.dagger', 'hilt-android').versionRef('hilt')
            library('coil.compose', 'io.coil-kt', 'coil-compose').version('2.3.0')
            library('retrofit2.retrofit', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('okhttp3.okhttp', 'com.squareup.okhttp3', 'okhttp').versionRef('okhttp3')
            library('okhttp3.loggingInterceptor', 'com.squareup.okhttp3', 'logging-interceptor').versionRef('okhttp3')
            library('kotlinx.serialization.json', 'org.jetbrains.kotlinx', 'kotlinx-serialization-json-jvm').version('1.5.1')
            library('retrofit2.kotlinx.serialization.converter','com.jakewharton.retrofit','retrofit2-kotlinx-serialization-converter').version('1.0.0')
//          compiler libraries and bundles
            library('hilt.compiler', 'com.google.dagger', 'hilt-compiler').versionRef('hilt')
            library('hilt.android.compiler', 'com.google.dagger', 'hilt-android-compiler').versionRef('hilt')
            bundle('kaptAndroidTest', [
                    'hilt.android.compiler',
            ])
            bundle('kapt', [
                    'hilt.compiler',
            ])
            bundle('kaptTest', [
                    'hilt.android.compiler',
            ])

//          test libraries and bundles
            library('kotlinx.coroutines.test', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-test').version('1.7.3')
            library('junit', 'junit', 'junit').version('4.13.2')
            library('androidx.test.core', 'androidx.test', 'core').version('1.5.0')
            library('androidx.test.runner', 'androidx.test', 'runner').version('1.5.2')
            library('androidx.test.rules', 'androidx.test', 'rules').version('1.5.0')
            library('androidx.espresso.core', 'androidx.test.espresso', 'espresso-core').version('3.5.1')
            library('androidx.junit.ktx', 'androidx.test.ext', 'junit-ktx').version('1.1.5')
            library('mockito', 'org.mockito', 'mockito-core').version('5.3.1')
            library('mockito.kotlin', 'org.mockito.kotlin', 'mockito-kotlin').version('5.0.0')
            library('mockk', 'io.mockk', 'mockk').version('1.4.1')
            library('hamcrest', 'org.hamcrest', 'hamcrest').version('2.2')
            library('okhttp3.mockwebserver', 'com.squareup.okhttp3', 'mockwebserver').versionRef('okhttp3')
            library('hilt.android.testing', 'com.google.dagger', 'hilt-android-testing').versionRef('hilt')
            library('turbine', 'app.cash.turbine', 'turbine').version('1.0.0')

            bundle('testImpl', [
                    'kotlinx.coroutines.test',
                    'junit',
                    'androidx.test.core',
                    'mockito',
                    'mockito.kotlin',
                    'mockk',
                    'hamcrest',
                    'okhttp3.mockwebserver',
                    'turbine',
                    'kotlinx.serialization.json',
                    'hilt.android.testing',
            ])

            bundle('androidTestImpl', [
                    'kotlinx.coroutines.test',
                    'junit',
                    'androidx.test.core',
                    'androidx.test.runner',
                    'androidx.test.rules',
                    'androidx.espresso.core',
                    'androidx.junit.ktx',
                    'hilt.android.testing',
            ])


        }
    }
}
rootProject.name = "Crane"
include ':app'
include ':core:network'
include ':core:common'
include ':core:testing'
